#
# A theme based on the steeef and nicoulaj themes. (work in progress)
#
# Authors:
#   fabacino <fabacino@gmail.com>
#   Julien Nicoulaud <julien.nicoulaud@gmail.com>
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Features:
#   - One line.
#   - Username and machine name in the left prompt.
#   - VCS and python virtualenv information in the right prompt.
#   - No unnecessary whitespace in the right prompt.
#   - Fully compatible with dumb terminals.
#   - Uses a different color depending on if the last command succeeded/failed.
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function prompt_fbn_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="(${basic_branch_format}${fbn_colors[4]}%B${vcs_char}%%b%f)"
  else
    branch_format="(${basic_branch_format})"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  # Build prompt on the right side depending on the current directory.
  RPROMPT="${basic_rprompt}"

  # Add vcs_info to prompt, if present.
  if [[ ${#vcs_info_msg_0_} -gt 0 ]]; then
    RPROMPT="${RPROMPT} "'${vcs_info_msg_0_}'
  fi

  # Add python virtualenv info to prompt, if present.
  if (( $+functions[python-info] )); then
    python-info
    RPROMPT="${RPROMPT} "'$python_info[virtualenv]'
  fi
}

function prompt_fbn_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook to adjust format for the right prompt.
  add-zsh-hook precmd prompt_fbn_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    fbn_colors=(
      "%F{37}"  # cyan
      "%F{136}" # yellow
      "%F{33}"  # blue
      "%F{160}" # red
      "%F{64}"  # green
    )
  else
    fbn_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{blue}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Use colors sparingly when in dumb terminal. Also avoid unicode characters.
  if [[ "$TERM" == (linux|dumb|*bsd*) ]]; then
    local user='%n'
    local machine='%m'
    local char='>'
    vcs_char='*'
  else
    local user='${fbn_colors[3]}%n%f'
    local machine='${fbn_colors[2]}%m%f'
    local char='❯'
    vcs_char='●'
  fi

  # Customizable parameters.
  local success_color='${fbn_colors[5]}'
  local failure_color='${fbn_colors[4]}'

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  basic_branch_format="${fbn_colors[1]}%b%f%u%c"
  local branch_format="(${basic_branch_format})"
  local action_format="(${fbn_colors[5]}%a%f)"
  local unstaged_format="${fbn_colors[2]}%B${vcs_char}%b%f"
  local staged_format="${fbn_colors[5]}%B${vcs_char}%b%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

  # Basic prompt on the right side contains only the current path. Additional
  # information might get added in the hook function.
  basic_rprompt="${fbn_colors[5]}%~%f"

  # Define prompts.
  PROMPT="${user}@${machine}%(?.${success_color}.${failure_color})${char}%b%f "
  RPROMPT="${basic_rprompt}"
}

prompt_fbn_setup "$@"
