#
# A theme based on the steeef and nicoulaj themes. (work in progress)
#
# Authors:
#   fabacino <fabacino@gmail.com>
#   Julien Nicoulaud <julien.nicoulaud@gmail.com>
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function prompt_fbn_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="(${fbn_colors[1]}%b%f%u%c${fbn_colors[4]}●%f)"
  else
    branch_format="(${fbn_colors[1]}%b%f%u%c)"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_fbn_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_fbn_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    fbn_colors=(
      "%F{37}"  # cyan
      "%F{136}" # yellow
      "%F{33}"  # blue
      "%F{160}" # red
      "%F{64}"  # green
    )
  else
    fbn_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{blue}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Customizable parameters.
  local success_color='${fbn_colors[5]}'
  local failure_color='${fbn_colors[4]}'

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${fbn_colors[1]}%b%f%u%c)"
  local action_format="(${fbn_colors[5]}%a%f)"
  local unstaged_format="${fbn_colors[2]}%B●%b%f"
  local staged_format="${fbn_colors[5]}%B●%b%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

  # Define prompts.
  PROMPT="${fbn_colors[3]}%n%f@${fbn_colors[2]}%m%f%(?.${success_color}.${failure_color})>%b%f "
  RPROMPT="${fbn_colors[5]}%~%f "'${vcs_info_msg_0_}'" "'$python_info[virtualenv]'
}

prompt_fbn_setup "$@"
